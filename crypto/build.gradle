plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-parcelize'
    id 'kotlinx-serialization'
}

android {
    compileSdkVersion 31
    buildToolsVersion "31.0.0"

    defaultConfig {
        minSdkVersion 23
        targetSdkVersion 31

        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    task fetchGdk (type: Exec) {
        workingDir '.'
        commandLine './fetch_gdk_binaries.sh'
    }
}

afterEvaluate {
    android.libraryVariants.all { variant ->
        def jniLibs = new File('./crypto/src/main/jniLibs')
        if (!jniLibs.exists()) {
            preBuild.dependsOn fetchGdk
        }
    }
}

dependencies {
    /**  --- Kotlin & KotlinX ------------------------------------------------------------------- */
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-core:$kotlinx_serialization_version"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:$kotlinx_serialization_version"
    /** ----------------------------------------------------------------------------------------- */
    
    /**  --- AndroidX --------------------------------------------------------------------------- */
    implementation 'androidx.annotation:annotation:1.4.0'
    /** ----------------------------------------------------------------------------------------- */

    /**  --- Lifecycle -------------------------------------------------------------------------- */
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.5.0"
    /** ----------------------------------------------------------------------------------------- */

    /**  --- RxJava  ---------------------------------------------------------------------------- */
    implementation "io.reactivex.rxjava3:rxjava:3.0.12"
    implementation "io.reactivex.rxjava3:rxkotlin:3.0.1"
    /** ----------------------------------------------------------------------------------------- */

    /**  --- Logging ---------------------------------------------------------------------------- */
    implementation 'org.slf4j:slf4j-android:1.7.30'
    implementation 'io.github.microutils:kotlin-logging:1.12.0'
    /** ----------------------------------------------------------------------------------------- */

    /**  --- Testing ---------------------------------------------------------------------------- */
    testImplementation 'junit:junit:4.13.2'
    /** ----------------------------------------------------------------------------------------- */
}